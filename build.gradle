plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'info.solidsoft.pitest' version '1.7.0'
}

group 'com.montealegreluis'
version '1.0-SNAPSHOT'

apply from: rootProject.file('hooks.gradle')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/assert")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/service-buses")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    implementation "io.vavr:vavr:0.10.4"
    implementation 'com.montealegreluis:assert:1.1.0'
    api 'com.montealegreluis:service-buses:5.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation testFixtures('com.montealegreluis:service-buses:5.0.0')
    testImplementation testFixtures('com.montealegreluis:activity-feed:3.1.1')
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-inline:4.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testFixturesImplementation testFixtures('com.montealegreluis:service-buses:5.0.0')
}

test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

jacocoTestReport {
    dependsOn test
    reports {
        html.destination file("${buildDir}/coverage")
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.15'
    coverageThreshold = 97
    mutationThreshold = 98
}

